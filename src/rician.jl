#### Utilities

@inline promote_float(x...) = promote(map(float, x)...)

#### Rician negative log-likelihood

@inline function neglogpdf_rician(x::T, Î½::T, logÏƒ::T) where {T <: Real}
    Ïƒâ»Â¹ = exp(-logÏƒ)
    return logÏƒ + neglogpdf_rician(Ïƒâ»Â¹ * x, Ïƒâ»Â¹ * Î½)
end
@inline neglogpdf_rician(x, Î½, logÏƒ) = neglogpdf_rician(promote_float(x, Î½, logÏƒ)...)

@inline neglogpdf_rician(x::T, Î½::T) where {T <: Union{Float32, Float64}} = (x - Î½)^2 / 2 - log(x) - logbesseli0x(x * Î½) # negative Rician log-likelihood `-logp(x | Î½, Ïƒ = 1)`

@inline function âˆ‡neglogpdf_rician(x::T, Î½::T) where {T <: Union{Float32, Float64}}
    # Define the univariate normalized Bessel function `IÌ‚â‚€` as
    #
    #   IÌ‚â‚€(z) = Iâ‚€(z) / (exp(z) / âˆš2Ï€z).
    #
    # The negative likelihood is then be written as
    #
    #        -logp(x | Î½, Ïƒ = 1) = (x - Î½)^2 / 2 - log(x / Î½) / 2 - logIÌ‚â‚€(x * Î½) + logâˆš2Ï€.
    #   âˆ‚/âˆ‚x -logp(x | Î½, Ïƒ = 1) = x - Î½ - 1 / 2x - âˆ‚/âˆ‚x logIÌ‚â‚€(x * Î½).
    #   âˆ‚/âˆ‚Î½ -logp(x | Î½, Ïƒ = 1) = Î½ - x + 1 / 2Î½ - âˆ‚/âˆ‚Î½ logIÌ‚â‚€(x * Î½).
    #
    # All that must be approximated then is `d/dz logIÌ‚â‚€(z)` where `z = x * Î½`:
    #
    #   d/dz logIÌ‚â‚€(z) =  1/2z + (Iâ‚(z) / Iâ‚€(z) - 1)
    #                 â‰ˆ -1/8z^2 - 1/8z^3 - 25/128z^4 - 13/32z^5 - 1073/1024z^6 - 103/32z^7 + ğ’ª(1/z^8)   (z >> 1)
    #                 â‰ˆ  1/2z - 1 + z/2 - z^3/16 + z^5/96 - 11*z^7/6144 + ğ’ª(z^9)                        (z << 1)
    #   âˆ‚/âˆ‚x logIÌ‚â‚€(z) = Î½ * d/dz logIÌ‚â‚€(z)
    #   âˆ‚/âˆ‚Î½ logIÌ‚â‚€(z) = x * d/dz logIÌ‚â‚€(z)

    # Note: there are really three relevant limits: z << 1, z >> 1, and x â‰ˆ Î½.
    # Could plausibly better account for the latter case, though it is tested quite robustly
    z = x * Î½
    if z < besseli1i0_low_cutoff(T)
        zÂ² = z^2
        r = z * evalpoly(zÂ², besseli1i0_low_coefs(T)) # r = logIÌ‚â‚€â€²(z) + 1 - 1/2z = Iâ‚(z) / Iâ‚€(z) â‰ˆ z/2 + ğ’ª(z^3)
        âˆ‚x = x - Î½ * r - inv(x)
        âˆ‚Î½ = Î½ - x * r
    elseif z < besseli1i0_mid_cutoff(T)
        zÂ² = z^2
        r = z * evalpoly(zÂ², besseli1i0_mid_num_coefs(T)) / evalpoly(zÂ², besseli1i0_mid_den_coefs(T)) # r = Iâ‚(z) / Iâ‚€(z)
        âˆ‚x = x - Î½ * r - inv(x)
        âˆ‚Î½ = Î½ - x * r
    elseif z < besseli1i0_high_cutoff(T)
        zÂ² = z^2
        r = z * evalpoly(zÂ², besseli1i0_high_num_coefs(T)) / evalpoly(zÂ², besseli1i0_high_den_coefs(T)) # r = Iâ‚(z) / Iâ‚€(z)
        âˆ‚x = x - Î½ * r - inv(x)
        âˆ‚Î½ = Î½ - x * r
    else
        zâ»Â¹ = inv(z)
        tmp = zâ»Â¹ * evalpoly(zâ»Â¹, besseli1i0c_tail_coefs(T)) # -z * logIÌ‚â‚€â€²(z) = -1/2 - z * (Iâ‚(z) / Iâ‚€(z) - 1) â‰ˆ 1/8z + ğ’ª(1/z^2)
        âˆ‚x = x - Î½ - (T(0.5) - tmp) / x
        âˆ‚Î½ = Î½ - x + (T(0.5) + tmp) / Î½
    end

    return (âˆ‚x, âˆ‚Î½)
end

@inline pdf_rician(args...) = exp(-neglogpdf_rician(args...))
@inline âˆ‡pdf_rician(x::T, Î½::T) where {T <: Union{Float32, Float64}} = -exp(-neglogpdf_rician(x, Î½)) .* âˆ‡neglogpdf_rician(x, Î½)

@scalar_rule neglogpdf_rician(x, Î½) (âˆ‡neglogpdf_rician(x, Î½)...,)
@dual_rule_from_frule neglogpdf_rician(x, Î½)

#### Rician negative log-cdf

# CDF is approximated by an integral of the Rician PDF over `(x, x+Î´)` using Gauss-Legendre quadrature.
# Consequently, PDF is never evaluated at the endpoints.
@inline function neglogcdf_rician(x::T, Î½::T, logÏƒ::T, Î´::T, order::Val) where {T <: Real}
    Ïƒâ»Â¹ = exp(-logÏƒ)
    return neglogcdf_rician(Ïƒâ»Â¹ * x, Ïƒâ»Â¹ * Î½, Ïƒâ»Â¹ * Î´, order)
end
@inline neglogcdf_rician(x, Î½, logÏƒ, Î´, order::Val) = neglogcdf_rician(promote_float(x, Î½, logÏƒ, Î´)..., order)

@inline neglogcdf_rician(x::T, Î½::T, Î´::T, order::Val) where {T <: Union{Float32, Float64}} = neglogf_quadrature(Base.Fix2(neglogpdf_rician, Î½), x, Î´, order)
@inline âˆ‡neglogcdf_rician(x::T, Î½::T, Î´::T, order::Val) where {T <: Union{Float32, Float64}} = âˆ‡neglogcdf_rician_with_primal(x, Î½, Î´, order)[2]

@inline function âˆ‡neglogcdf_rician_with_primal(Î©::T, x::T, Î½::T, Î´::T, order::Val) where {T <: Union{Float32, Float64}}
    âˆ‚x, âˆ‚Î½ = f_quadrature(x, Î´, order) do y
        âˆ‡ = âˆ‡neglogpdf_rician(y, Î½) # differentiate the integrand
        âˆ‡ = SVector{2, T}(âˆ‡)
        return exp(Î© - neglogpdf_rician(y, Î½)) * âˆ‡
    end
    âˆ‚Î´ = -exp(Î© - neglogpdf_rician(x + Î´, Î½)) # by fundamental theorem of calculus
    return Î©, (âˆ‚x, âˆ‚Î½, âˆ‚Î´)
end
@inline âˆ‡neglogcdf_rician_with_primal(x::T, Î½::T, Î´::T, order::Val) where {T <: Union{Float32, Float64}} = âˆ‡neglogcdf_rician_with_primal(neglogcdf_rician(x, Î½, Î´, order), x, Î½, Î´, order)

@scalar_rule neglogcdf_rician(x, Î½, Î´, order::Val) (âˆ‡neglogcdf_rician_with_primal(Î©, x, Î½, Î´, order)[2]..., NoTangent())
@dual_rule_from_frule neglogcdf_rician(x, Î½, Î´, !order)

#### Gauss-Legendre quadrature

const DEFAULT_GAUSSLEGENDRE_ORDER = 16

@generated function gausslegendre_unit_interval(::Val{order}, ::Type{T}) where {order, T <: AbstractFloat}
    x, w = gausslegendre(order)
    x = SVector{order, T}(@. T((1 + x) / 2)) # rescale from [-1, 1] to [0, 1]
    w = SVector{order, T}(@. T(w / 2)) # adjust weights to account for rescaling
    return :($x, $w)
end

@inline function f_quadrature(f::F, xâ‚€::T, Î´::T, ::Val{order} = Val(DEFAULT_GAUSSLEGENDRE_ORDER)) where {F, order, T <: AbstractFloat}
    x, w = gausslegendre_unit_interval(Val(order), T)
    y = @. f(xâ‚€ + Î´ * x)
    return vecdot(w, y) * Î´
end

@inline function neglogf_quadrature(neglogf::F, xâ‚€::T, Î´::T, ::Val{order} = Val(DEFAULT_GAUSSLEGENDRE_ORDER)) where {F, order, T <: AbstractFloat}
    x, w = gausslegendre_unit_interval(Val(order), T)
    logy = @. -neglogf(xâ‚€ + Î´ * x)
    return -weighted_logsumexp(w, logy) .- log(Î´)
end

@inline function weighted_logsumexp(w::SVector{N, T}, logy::SVector{N, T}) where {N, T <: AbstractFloat}
    max_ = maximum(logy)
    yÌ„ = exp.(logy .- max_)
    return log(vecdot(w, yÌ„)) + max_
end

@inline function weighted_logsumexp(w::SVector{N, T}, logy::SVector{N, SVector{M, T}}) where {N, M, T <: AbstractFloat}
    max_ = reduce(BroadcastFunction(max), logy) # elementwise maximum
    logy = reduce(hcat, logy) # stack as columns
    yÌ„ = exp.(logy .- max_)
    return log.(vecdot(w, yÌ„)) .+ max_
end

@inline vecdot(w::SVector{N, T}, y::SVector{N, T}) where {N, T <: AbstractFloat} = dot(w, y)
@inline vecdot(w::SVector{N, T}, y::SVector{N, SVector{M, T}}) where {N, M, T <: AbstractFloat} = vecdot(w, reduce(hcat, y))
@inline vecdot(w::SVector{N, T}, y::SMatrix{M, N, T}) where {N, M, T <: AbstractFloat} = y * w
